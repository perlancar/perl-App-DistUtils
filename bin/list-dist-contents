#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;

#use App::DistUtils;
use Dist::Util ();
use Perinci::CmdLine::Any -prefer_lite=>1;

our %SPEC;

$SPEC{list_dist_contents} = {
    v => 1.1,
    summary => 'List contents of an installed Perl distribution',
    args => {
        dist => {
            schema => 'str*',
            req => 1,
            pos => 0,
            completion => sub {
                # XXX move to App::DistUtils to avoid duplication
                require Complete::Util;

                my %args = @_;

                my $word = $args{word} // '';
                $word =~ s!/|::!-!g;
                Complete::Util::complete_array_elem(
                    word=>$word,
                    array=>[Dist::Util::list_dists],
                    ci=>1,
                );
            },
        },
    },
    "cmdline.default_format" => 'text-simple',
};
sub list_dist_contents {
    my %args = @_;

    my $dist = $args{dist};
    $dist =~ s/-/::/g;
    my $packlist = Dist::Util::packlist_for($dist);
    return [404, "Can't find .packlist for $dist"] unless $packlist;
    [200, "OK", do { local $/; open my($fh), "<", $packlist; ~~<$fh> }];
}

Perinci::CmdLine::Any->new(
    url => "/main/list_dist_contents",
)->run;

# ABSTRACT: List contents of an installed Perl distribution
# PODNAME: list-dist-contents

=head1 SYNOPSIS

 % list-dist-contents Text-ANSITable


=head1 DESCRIPTION

=cut
